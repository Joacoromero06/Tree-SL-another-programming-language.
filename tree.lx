%option noyywrap
%{
    #include "data.h"
    #include "ast.h"
    #include "env.h"
    #include "tree.tab.h"
%}

EXP ([eE][+-]?[0-9]+)
%s REPL

%%
"!" |
"&" |
"%" | 
";" |
":" |
"|" |
"=" |
"," |
"(" |
")" |
"[" |
"]" |
"{" |
"}" |
"+" | 
"-" | 
"*" |
"/" {  return yytext[0]; }

">"   { return T_MAYOR; }
"<"   { return T_MENOR; }
">="  { return T_MAYOR_IGUAL; }
"<="  { return T_MENOR_IGUAL; }
"=="  { return T_IGUAL; }
"!="  { return T_DISTINTO; }

"=>"  { return T_FLECHA;  }

and   { return T_AND; }
or    { return T_OR; }
not   { return T_NOT; }

if        { return T_IF;      }
else      { return T_ELSE;    }
endif     { return T_ENDIF;   }
while     { return T_WHILE;   }
do        { return T_DO;      }
end       { return T_END;     }
forall    { return T_FORALL;  }
forany    { return T_FORANY;  }

"function"  { return T_FUNCTION;  }
procedure { return T_PROCEDURE; }
endfn     { return T_ENDFN;     } 
ref       { return T_REF;       } 

main     { return T_MAIN;       }
endmain  { return T_ENDMAIN;    }
"$tree"  { BEGIN(REPL); return T_PESOS_TREE; }

print       { return T_PRINT;   }
println     { return T_PRINTLN; }
return      { return T_RETURN;  }
break       { return T_BREAK;   }
"continue"  { return T_CONTINUE;}
let         { return T_LET;     }

kick   { return T_KICK;   }
add    { return T_ADD;    }
take   { return T_TAKE;   }
from   { return T_FROM;   }
to     { return T_TO;     }
concat { return T_CONCAT; }

in        { return T_IN; }
union     { return T_UNION; }
inter     { return T_INTER; }
diff      { return T_DIFF;  }
\get       { return T_GET; }
\contains { return T_CONTAINS; }

"true" |
"false" { yylval.td = createBool(strdup(yytext)); return T_BOOL; }

[0-9]+                    { yylval.td = createInt(atoi(strdup(yytext))); return NUM_INT; }
\".*\"        { yylval.td = createStr(strdup(yytext)); return ATOM; }
[0-9]+"."?[0-9]*{EXP}? |
[0-9]*"."?[0-9]+{EXP}?    { yylval.td = createDouble(atof(strdup(yytext))); return NUM_DOUBLE; }
[a-zA-Z0-9_]+              { yylval.s = strdup(yytext); return ID; }  
"#".*  {}
[ \t]  {} 
<REPL>\n { return EOL; }
\n  {}
.   { printf("CARACTER DESCONOCIDO: %s\n",yytext); }

%%